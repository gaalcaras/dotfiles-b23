" ############################
"
" vimrc by @gaalcaras
"
" Meant to be used with NeoVim
" Autoload functions --> $HOME/.dotfiles/vim/dotfiles.vim

scriptencoding utf8

if has('nvim')
  set runtimepath+=/usr/share/vim/vimfiles
endif

if has('autocmd')
  filetype plugin indent on
endif

if has('syntax') && !exists('g:syntax_on')
  syntax enable
endif

" ================
" General settings
" ================

let g:maplocalleader = ';'
let g:mapleader = ','

set foldmethod=syntax
set foldlevelstart=1
set splitright " Open splits on the right
set splitbelow " Open splits on the bottom
set wildignore=*.pdf,*.aux,*.png,*.pyc
set wildmode=longest,list,full
set modeline

set linebreak " Wrap lines smartly (not always in the middle of a word)
set nolist " Disable list (otherwise, breaks linebreak)
set formatoptions=qrn1tj " Add options for wrapping (see help for details)
silent! set breakindent showbreak=.. " Visually show soft breaks

set list listchars=tab:»·,trail:·,nbsp:· " Display extra whitespace
set shiftwidth=2 tabstop=2 softtabstop=2 expandtab
set autoindent

" Normal colors don't work well with base16 and asciinema
if $ASCIINEMA_REC == "1"
  set termguicolors
endif

" Relative numbers
set relativenumber
set number
if has('nvim')
  au TermOpen * setlocal nonumber norelativenumber " Not in the terminal tho
endif

set cursorline

" Lowercase is case sensitive, upper case is not
set ignorecase
set smartcase

" Get rid of additionnal files
set nobackup
set nowritebackup
set noswapfile
set undofile
set undodir=~/.vim/undodir " But persistent undo is pretty rad

set nrformats-=octal " Do not consider number beginning with 0 as octal
set scrolloff=1 " Always show at least one line above or below current line
set sidescrolloff=5 " Keep 5 columns to the right or left of cursor
set linespace=3 " Prefer a slightly higher line height
set spell spelllang=fr,en " Enable both French and English spell check
set mouse-=a " Disable mouse visual mode

" Underline spell check results
hi clear SpellBad
hi SpellBad cterm=underline

set path+=** " Make file search recursive in dirs and subdirs

let g:netrw_banner=0 " Disable annoying banner
let g:netrw_browse_split=4 " Open in prior window
let g:netrw_altv=1 " Open splits to the right

" Tree view
let g:netrw_liststyle=3
let g:netrw_list_hide=netrw_gitignore#Hide()
let g:netrw_list_hide.=',\(^\|\s\s\)\zs\.\S\+'

" Keep selection when changing indentation in visual mode
vnoremap < <gv
vnoremap « <gv
vnoremap > >gv
vnoremap » >gv

" Auto save buffers
augroup AutoSaveBuffers
  autocmd!

  autocmd! BufLeave,InsertLeave,CursorHold * silent! :w
  autocmd! FocusLost,FocusGained * silent! :wa
augroup END

" ==================================
" Personal mappings and text objects
" ==================================

" Show next matched string at the center of screen
nnoremap n nzz
nnoremap N Nzz

" Escape from inside [], (), {}, «», '', "", **
inoremap <c-e> <esc>/[)}*"»'`\]*]<cr>:nohl<cr>a
nnoremap <c-e> /[)}*"»'`\]*]<cr>:nohl<cr>a

" Easier tag navigation
nnoremap <c-m> <c-]>
vnoremap <c-m> <c-]>

nnoremap gf :w<cr>gf
" Open file under cursor in vertical split
nnoremap g<cr> :w<cr><c-w>vgf
" Open file under cursor in horizontal split
nnoremap g<space> :w<cr><c-w>f

nnoremap <c-s> :w<cr>
inoremap <c-s> <c-o>:w<cr>

" Append the line above to the end
nnoremap <leader>tt :m-2<cr>:j<cr>
" Delete everything right of the cursor and paste it in a new line
" line above, then delete beginning and trailing whitespaces
nnoremap <leader>ts DO<Esc>p:.s/^\s\+//<cr>:+1<cr>:.s/\s\+$//<cr>$
" Add a line of = below
nnoremap <leader>= yyp:s/./=/g<cr>:noh<cr>
" Add a line of - below
nnoremap <leader>- yyp:s/./-/g<cr>:noh<cr>
" Align with spaces
nnoremap <leader>$ :call dotfiles#AlignWithSpaces(78)<cr>

" Open fuzzy file search
nnoremap <leader>, :Denite file_rec<cr>
" Open fuzzy buffer search
nnoremap <leader>b :Denite buffer<cr>
nnoremap <leader><cr> :vsplit<cr> :Denite file_rec<cr>
" Edit vimrc
nnoremap <leader>ve :edit ~/.dotfiles/vim/vimrc.symlink<cr>
" Reload vimrc
nnoremap <leader>vr :w<cr>:source $MYVIMRC<cr>

" Clear highlighted search results
nnoremap <silent> <c-L> :nohlsearch<cr>
nnoremap <silent> <localleader>B :call dotfiles#ToggleSolarizedDarkTheme()<cr>

" Sorry ExMode, but you've gotta go
map q: <Nop>
nnoremap Q <nop>

" Add new text objects delimited by :
call dotfiles#DefineNewTextObjects([ '_', '.', ':', ',', ';', '<bar>', '/',
      \ '<bslash>', '*', '+', '%', '-', '#' ])

" ========
" vim-plug
" ========

call plug#begin('~/.vim/plugged')

" Language related plugins
" ------------------------

" Markdown
Plug 'euclio/vim-markdown-composer', {'do':function('dotfiles#BuildComposer'),
      \ 'for' : 'markdown' }
Plug 'plasticboy/vim-markdown', { 'for': 'markdown' }
Plug 'mzlogin/vim-markdown-toc', { 'for': 'markdown' }
Plug 'junegunn/goyo.vim', { 'for': ['markdown', 'tex'] } " Focused writing

" Python
Plug 'tmhedberg/simpylfold', { 'for': 'python' } " Adequate Python folding
Plug 'Vimjas/vim-python-pep8-indent', { 'for': 'python' }
Plug 'bfredl/nvim-ipy', { 'for': 'python', 'do': ':UpdateRemotePlugins' }

" Other programming languages
Plug 'pangloss/vim-javascript', { 'for': ['javascript', 'typescript'] }
Plug 'lervag/vimtex', { 'for': ['latex', 'tex', 'rnoweb'] }
Plug 'jalvesaq/Nvim-R', { 'for': ['r', 'rmd', 'rnoweb', 'rrst'] }
Plug 'Shougo/neco-vim', { 'for': 'vim' }
Plug 'tpope/vim-endwise', { 'for': ['ruby', 'vim', 'lua'] }

" Language completion and syntax checking
Plug 'w0rp/ale' " Check style and syntax asynchronously
Plug 'roxma/nvim-completion-manager', { 'on': [] }
if !has('nvim')
    Plug 'roxma/vim-hug-neovim-rpc'
endif
Plug 'roxma/ncm-github', { 'for': 'markdown' }
Plug 'SirVer/ultisnips', { 'on': [] }
Plug 'honza/vim-snippets'

" General functionnality
" ----------------------

Plug 'tpope/vim-repeat'
Plug 'blindFS/vim-taskwarrior', { 'on': 'TW' }
Plug 'neomutt/neomutt.vim'
Plug 'Shougo/denite.nvim', { 'do' : ':UpdateRemotePlugins' }
Plug 'xtal8/traces.vim' " Range and pattern preview for Ex commands
Plug 'tmux-plugins/vim-tmux-focus-events'

" Give vim some note taking abilities
Plug 'xolox/vim-misc'
Plug 'xolox/vim-notes'

" Git
Plug 'tpope/vim-fugitive'
Plug 'airblade/vim-gitgutter'
Plug 'jreybert/vimagit', { 'on': 'Magit' }

" Colors and appearance
Plug 'chriskempson/base16-vim/', { 'commit': 'a808330' }
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'
Plug 'edkolev/tmuxline.vim'

" Improve text editing
" --------------------

Plug 'machakann/vim-sandwich'
Plug 'ConradIrwin/vim-bracketed-paste' " Set 'set:paste' in insert mode
Plug 'scrooloose/nerdcommenter'
Plug 'raimondi/delimitmate' " Handle delimiters automatically
Plug 'tpope/vim-abolish' " Add improved support for abbreviations
Plug 'junegunn/vim-easy-align'
Plug 'yangmillstheory/vim-snipe'

" My own plugins
" --------------

Plug '~/Codecave/transcribe.nvim', { 'for': ['text', 'markdown'] }
Plug '~/Codecave/ncm-R'
" Plug 'gaalcaras/ncm-R'
" Plug 'gaalcaras/transcribe.nvim', { 'do': './install' }

" Load these plugins only when entering Insert Mode
augroup load_on_insert
  autocmd!
  autocmd InsertEnter * call plug#load('ultisnips', 'nvim-completion-manager')
        \| autocmd! load_on_insert
augroup END

call plug#end() " Add plugins to &runtimepath

" =======
" Plugins
" =======

" vim-snipe
nmap <leader>sf <Plug>(snipe-f)
nmap <leader>sF <Plug>(snipe-F)
nmap <leader>st <Plug>(snipe-t)
nmap <leader>sT <Plug>(snipe-T)
nmap <leader>sé <Plug>(snipe-w)
nmap <leader>sÉ <Plug>(snipe-W)
nmap <leader>sb <Plug>(snipe-b)
nmap <leader>sB <Plug>(snipe-B)
nmap <leader>sgé <Plug>(snipe-ge)
nmap <leader>sgÉ <Plug>(snipe-gE)
nmap <leader>sx <Plug>(snipe-f-xp)
nmap <leader>sX <Plug>(snipe-F-xp)
let g:snipe_jump_tokens = 'auiectsrnmbépo'

" vimagit
let g:magit_stage_line_mapping = 'LL'

" vim-sandwich with surround mappings
let g:sandwich_no_default_key_mappings = 1
let g:operator_sandwich_no_default_key_mappings = 1
let g:textobj_sandwich_no_default_key_mappings = 1
nmap ys <Plug>(operator-sandwich-add)
nmap ls <Plug>(operator-sandwich-replace)
      \<Plug>(operator-sandwich-release-count)
      \<Plug>(textobj-sandwich-query-a)
nmap ds <Plug>(operator-sandwich-delete)
      \<Plug>(operator-sandwich-release-count)
      \<Plug>(textobj-sandwich-query-a)

" Denite
call denite#custom#source('file_rec', 'matchers',
      \ ['matcher_fuzzy', 'matcher_ignore_globs'])
call denite#custom#filter('matcher_ignore_globs', 'ignore_globs',
      \ [ '.git/', '.ropeproject/', '__pycache__/',
      \   '*.png', '*.pdf', '*.jpg', '*.aux', '*.out',
      \   '.Rproj.user/*', '.~lock*', '.Rhistory',
      \   'venv/', 'images/', '*.min.*', 'img/', 'fonts/'])

call denite#custom#map(
      \ 'insert',
      \ '<C-n>',
      \ '<denite:move_to_next_line>',
      \ 'noremap'
      \)

call denite#custom#map(
      \ 'insert',
      \ '<C-p>',
      \ '<denite:move_to_previous_line>',
      \ 'noremap'
      \)

" Solarized via base16
if filereadable(expand('~/.vimrc_background'))
  let g:base16colorspace=256
  source ~/.vimrc_background
endif

" nvim-completion-manager
set shortmess+=c

" Airline
let g:airline_powerline_fonts = 1
let g:airline_theme = 'base16'
let g:airline#extensions#tabline#enabled = 1 " Display buffers as tabs on top
let g:airline_detect_spelllang=0
let g:airline_detect_spell=0
let g:airline#extensions#hunks#enabled=0
let g:airline#extensions#wordcount#enabled=1
let g:airline#parts#ffenc#skip_expected_string='utf-8[unix]'
let g:airline_section_z=':%c'

" Delimitmate
let g:delimitMate_expand_cr = 1 " Correct indentation

" Nvim-R
let g:r_syntax_folding = 1
let g:R_assign = 0 " Do not expand _ to <-
let g:R_clear_line = 1 " Always clear line in the R Console
let R_start_libs = "base,stats,graphics,grDevices,utils,methods,datasets"

" Vim Markdown
let g:vim_markdown_folding_level = 2
let g:vim_markdown_new_list_item_indent = 2
let g:vim_markdown_frontmatter = 1

" Vim Markdown Composer
let g:markdown_composer_open_browser = 0

" vimtex
" let g:vimtex_complete_recursive_bib=1
let g:vimtex_fold_enabled = 1
let g:vimtex_quickfix_latexlog = {'fix_paths':0}

augroup vimtex_config
  au!
  au User VimtexEventQuit     VimtexClean " Clean aux files after quitting
  au User VimtexEventInitPost VimtexCompile " Open pdf when opening
augroup END

" Remap these to avoid conflict with BÉPO mapping (t -> è)
nnoremap èsD <Plug>(vimtex-delim-toggle-modifier-reverse)
nnoremap èsd <Plug>(vimtex-delim-toggle-modifier)
nnoremap èsc <Plug>(vimtex-cmd-toggle-star)
nnoremap èse <Plug>(vimtex-env-toggle-star)

" Fugitive
set diffopt+=vertical " Force vertical split event on small screens
let g:fugitive_git_executable = 'LANG=en_US git' " Force git english

" Gitgutter
nnoremap <c-n> :GitGutterNextHunk<CR>
nnoremap <c-p> :GitGutterPrevHunk<CR>
nnoremap <c-u> :GitGutterUndoHunk<CR>

" vim-notes
let g:notes_directories = ['~/Zettelkasten/zettel']
let g:notes_new_note_template = '~/Zettelkasten/new_zettel'
let g:notes_title_sync = 'rename_file'

let g:notes_smart_quotes = 0

" Transcribe
" imap <silent><buffer> <C-S> <plug>(transcribe-speed-inc)
" nmap <silent><buffer> <leader>s <plug>(transcribe-speed-inc)
" imap <silent><buffer> <C-t> <plug>(transcribe-speed-dec)
" nmap <silent><buffer> <leader>t <plug>(transcribe-speed-dec)
" imap <silent><buffer> <C-c> <plug>(transcribe-seek-backward)
" nmap <silent><buffer> <leader>c <plug>(transcribe-seek-backward)
" imap <silent><buffer> <C-r> <plug>(transcribe-seek-forward)
" nmap <silent><buffer> <leader>r <plug>(transcribe-seek-forward)
" imap <silent><buffer> <C-v> <plug>(transcribe-timepos-get)
" imap <silent><buffer> <C-p> <plug>(transcribe-sync)

" vim-easy-align
vmap <Enter> <Plug>(LiveEasyAlign)
nmap ga <Plug>(LiveEasyAlign)

" nerdcommenter
let g:NERDSpaceDelims = 1
let g:NERDDefaultAlign = 'left'
let g:NERDTrimTrailingWhitespace = 1

" ALE
nmap <silent> <C-t> <Plug>(ale_previous_wrap)
nmap <silent> <C-s> <Plug>(ale_next_wrap)
nmap <silent> <leader>f <Plug>(ale_fix)

let g:ale_fixers = ['autopep8', 'yapf'] " Fix Python files
let g:ale_linters = {
      \'latex': ['chktex'],
      \}

" UltiSnips
let g:UltiSnipsExpandTrigger='<tab>'
let g:UltiSnipsListSnippets='<c-tab>'
let g:UltiSnipsJumpForwardTrigger='<tab>'
let g:UltiSnipsJumpBackwardTrigger='<s-tab>'
let g:UltiSnipsEditSplit='vertical'
let g:UltiSnipsSnippetsDir='.vim/myownsnips'
let g:UltiSnipsSnippetDirectories=['UltiSnips', 'myownsnips']

" nvim-ipy (mappings in ftplugin)
let g:nvim_ipy_perform_mappings = 0

" ==================
" French bépo layout
" ==================

" Text objects
noremap é w
noremap É W
onoremap aé aw
onoremap aÉ aW
onoremap ié iw
onoremap iÉ iW

" Easier pane navigation
noremap w <C-w>
noremap W <C-w><C-w>
noremap wt <C-w>j
noremap ws <C-w>k
noremap wc <C-w>h
noremap wr <C-w>l
noremap wd <C-w>c
noremap wo <C-w>s
noremap wp <C-w>o
noremap w<SPACE> :split<CR>
noremap w<CR> :vsplit<CR>

" [HJKL] -> {CTSR}
noremap c h
noremap r l
noremap t j
noremap s k
noremap C H
noremap R L
noremap gs gk
noremap gt gj
noremap gb gT
noremap gé gt
noremap g" g0

" {JK} -> {TS}
noremap T J
noremap S K

" Screen shifting
noremap zs zb
noremap zt zt

" {J}
noremap j t
noremap J T

" {L}
noremap l c
noremap L C

" {H}
noremap h r
noremap H R

" {K}
noremap k s
noremap K S

" Spellcheck
noremap ]k ]s
noremap [k [s

" Indentation
noremap « <
noremap » >

" Netwr
augroup netrw_mapping
  autocmd!
  autocmd filetype netrw call NetrwMapping()
augroup END

function! NetrwMapping()
  noremap <buffer> c h
  noremap <buffer> r l
  noremap <buffer> t j
  noremap <buffer> s k
endfunction

" ========
" Hardmode
" ========

command! ToggleDisablingOfNonCountedBasicMotions
      \ :call dotfiles#ToggleDisablingOfBasicMotionsIfNonCounted()
command! DisableNonCountedBasicMotions
      \ :call dotfiles#SetDisablingOfBasicMotionsIfNonCounted(1)
command! EnableNonCountedBasicMotions
      \ :call dotfiles#SetDisablingOfBasicMotionsIfNonCounted(0)

augroup hardmode
  autocmd!
  autocmd VimEnter * DisableNonCountedBasicMotions
augroup END

" Never use arrow keys ;-)
noremap <Left> :echoe "Use c ;-)"<CR>
noremap <Right> :echoe "Use r ;-)"<CR>
noremap <Up> :echoe "Use s ;-)"<CR>
noremap <Down> :echoe "Use t ;-)"<CR>

" Say goodbye to backspace and delete in Insert mode
inoremap <BS> <Nop>
inoremap <Del> <Nop>
