# ZMV

autoload -U zmv

# VARS

export NOTES_FOLDER='/home/gabo/ownCloud/notes/md/'
export NOTES_FOLDER_PDF='/home/gabo/ownCloud/notes/pdf/'
export PATH=$HOME/bin:/usr/local/bin:$HOME/.gem/ruby/2.3.0/bin:/bin:/usr/local/bin:/usr/bin
export EDITOR="nvim"

# Shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# All of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# Load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if [[ -d ~/.zplug ]]
then
  #ZPLUG
  source ~/.zplug/init.zsh

  zplug "plugins/git", from:oh-my-zsh, if:"(( $+commands[git] ))"

  puredir="$HOME/.zplug/repos/sindresorhus/pure"
  zplug "mafredri/zsh-async", on:sindresorhus/pure
  zplug "sindresorhus/pure", use:pure.zsh, nice:19

  zplug "zsh-users/zsh-history-substring-search"

  # Install plugins if there are plugins that have not been installed
  if ! zplug check --verbose; then
      printf "Install? [y/N]: "
      if read -q; then
          echo; zplug install
      fi
  fi

  # Then, source plugins and add commands to $PATH
  zplug load
fi

# Load solarized dircolors
eval `dircolors $HOME/.dotfiles/zsh/dircolors`

bindkey "^[[A" history-beginning-search-backward
bindkey "^[[B" history-beginning-search-forward
